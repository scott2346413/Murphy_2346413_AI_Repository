title: Awake
tags:
---
<<declare $scene = 0>>

//ALIEN RACES
// The three main alien races are Phirakians, Glopites and Echolyths
// Phirakians are a hostile military race you are trying to make peace with
// Glopites are a friendly race of slime creatures
// Echolyths are an ancient, peaceful and spiritual people

// As you make peace with the phirakians you are given a choice to "screw over" the glopites
// This may cause the Echolyths to intervene
// by the end the three outcomes are war with Phira, war with Glopites and Echolyths, and neutrality with all three

//Deciders. Each scene decides one of these variables which determine the available endings in the final scene
<<declare $missionExplained = false>>
<<declare $phirakiansProposeAlliance = false>>
<<declare $agreeToHelpGlopites = false>>
<<declare $phirakiansInsulted = false>>

//Endings
//1 = Cold War. The Phirakians proceed to conquer Glopp and torment the Glopites for decades.
//Meanwhile, Earth and Phira enter a cold war, neither daring enough to provoke the other, remaining in constant fear of a new war
//2 = Continued War. The Phirakians fight hard against the humans and Glopp for many years.
//The War is long and sufferable. Eventually a peace breaks though both sides suffer heavily
//3 = A Sturdy Alliance. Glopp is betrayed by earth and the Phirakians take it over with ease. Earth even provides Phira with weapons
//Continuing on, Phira wage tyranny and cruelty across the galaxy, and earth profits greatly
//4 = Continued Talks. While Phira continue to assault Glopp, peace talks continue with earth. They reach a long and complex peace agreement
//which opens up trade and even introduces new rights to Phirakians. Although Phira are left free to continue assaulting helpless planets and earth may not intervene
//5 = End Of The World. Phira hold back on Glopp and redirect their full force against earth. The humans fight hard
//but are helpless against such overwhelming attacks. Phira soon wipes out earth and is free to take over the galaxy
//6 = Conquerers. Earth joins Phira as savage conquerers. They herald terror on Glopp and then across the galaxy. A rebellion alliance forms
//Amongst the surviving planets but it is crushed without hesitation. The Phira-Earth empire reigns corruption for centuries
//7 = The Liberation. A strong and thriving alliance is formed between Phira and Earth. Phira are respectful and more open
//to Earth's pleas to retreat from Glopp. After much work, the Phirakians agree to peace with Glopp too. As the years continue,
//both Phira and Earth become Utopias of peace and technology advances far beyond the rest of the galaxy. The people celebrate
<<declare $ending = 0>>

<<declare $typeOfHelp = "">>
===

title: Robot
tags:
---

<<if $scene == 0>>
<<jump RobotIntro>>
<<elseif $scene == 3>>
<<jump RobotFinal>>
<<endif>>
===

title: RobotIntro
tags:
---
Robot: Goodmorning Captain! Sleep Well? #line:GoodmorningCaptain
Robot: You were out a long time #line:RobotSound
-> Where am I? #line:0d5adf5 
    Robot: Do not panic. The cryo sleep has affected your memory
    Robot: You are aboard a space craft #line:0541f63
-> Who are you? #line:02718c4 
    Robot: Your sleeping pod has given you amnesia. I will make sure I fix that  #line:0cba7da
    Robot: I'm ADA, your Automated Digital Assistant! #line:009527b
-> What's going on? #line:0787c07 
    Robot: The memory unit is faulty on your sleeping pod. That explains your amnesia  #line:072230b
    Robot: You've just awoken from a 2 month cryosleep #line:03b35ab
    Robot: Our destination is the planet Phira. You are to make peace talks with the Phirakians #line:078d25b
    <<set $missionExplained to true>>

Robot: Allow me to formally catch you up. #line:09985ae
Robot: Welcome to the Scout! The fastest and greatest explorer vessel in the galaxy! #line:0224c5d
Robot: You are our designated captain! #line:004ce1d

<<if $missionExplained is true>>
-> Vessel? This doesn't look like a space craft? #line:0d49249 
    Robot: That's thanks to the PHOH - Projected Hologram Of Home #line:0e30281
    Robot: PHOH makes the space deck look a little more familiar to you #line:0946105
-> Will be be on Phira soon? #line:0a2f14a
    Robot: Affirmative. We will be in orbit any moment now #line:09ea11e
<<else>>
-> Vessel? You mean like a space craft? This doesn't look like a space craft though #line:08bd0a2
    Robot: That's thanks to the PHOH - Projected Hologram Of Home #line:06134fa
    Robot: PHOH makes the space deck look a little more familiar to you #line:0534114
-> Okay so, what exactly are we doing? #line:0e29b0e 
    Robot: Currently, we're closing in on the planet Phira #line:0c7e7e1
    Robot: You were chosen to make peace talks with the Phirakians #line:02ee28b
    <<set $missionExplained to true>>
<<endif>>

<<jump PhirakianEncounter>>
===

title: PhirakianEncounter
tags:
---
Robot: Hold on. Someone is requesting to board the ship #line:09830ec
Robot: They will be onboard imminently #line:0d6907c 
<<set $scene to 1>>
<<set_object_phirakian AlienSpawner>>
<<spawn_object AlienSpawner>>
<<toggle_collider Robot>>
===

title: PhirakianIntro
tags:
---
Phirakian: Greetings to you, human. My name is Pliink, the Phirakian representative #line:PhirakianGreeting 
Phirakian: I can only stay briefly so shall we begin #line:PhirakianSound
<<if $missionExplained == true>>
-> Welcome aboard, Pliink. Make yourself comfortable #line:0467ecf 
    Phirakian: You have my gratitude, Captain. Not all diplomats are as welcoming as you
    Phirakian: Perhaps Phira would be lucky to have an alliance with your colony after all #line:00cfc61
    <<set $phirakiansProposeAlliance to true>>
-> Hold on, why can I understand you? #line:0cecdad 
    Phirakian: Simple translation tech developed by Phirakian scientists #line:02e05bf
    Phirakian: We call it BUTT, Brilliant Universal Translation Technology #line:0801ca1
<<else>>
-> Sorry, who are you? I wasn't expecting any visitors #line:0bb56cb 
    Phirakian: Who am I? Were you not briefed or are you just stupid #line:053b4f3
    Phirakian: I made the effort to come on behalf of Phira for peace talks... #line:0eb5505
    Phirakian: And the captain doesn't even know why I'm here! #line:08d5a19
-> Welcome Pliink! What brings you to our ship? #line:00fee48 
    Phirakian: I was... invited... I'm the diplomat... #line:0e82822
    Phirakian: Don't tell me you humans are this forgetful #line:07ba4fe

-> I'm sorry if I've offended you. It's been a long journey. Remind me? #line:096ef08 
    Phirakian: I can forgive your insolance on regard of your efforts at least #line:0ddfaa3
    Phirakian: Earth and Phira have made the decision to commence peace talks #line:0052ecd
    Phirakian: After years of devesating war, we have come to offer an alliance #line:01d6bd6
    <<set $phirakiansProposeAlliance to true>>
-> Calm down! You're over reacting! #line:0ca5406 
    Phirakian: How dare you speak such offense, earthling #line:0b02d53
    Phirakian: I've had enough. I shall retreat to Phira and alert our leaders #line:04a0046
    Phirakian: I'm sure war is in your planet's future #line:0e21f8e
    <<set $phirakiansInsulted to true>>

<<set $missionExplained to true>>   
<<endif>>

Phirakian: I'm being called by Phira. I will return later #line:0e0d65a 

<<set $scene to 2>>
<<leave Phirakian>>
<<set_object_glopite AlienSpawner>>
<<spawn_object AlienSpawner>>
===

title: GlopiteIntro
tags:
---
Glopite: Human! Allow me to introduce the arrival of the Glopites! #line:GlopiteGreeting 
Glopite: I will pause so you may applaud #line:GlopiteSound
-> What brings the Glopite here? #line:087f0fe
    Glopite: War, human. Terrible war brings us here #line:09cccff #line:GlopiteSound
-> Greetings Glopites? How may we be of service #line:04176c8 #line:GlopiteSound
    Glopite: We require your help. We face great danger #line:0dfa95e #line:GlopiteSound
-> We weren't expecting your intrusion #line:0aa9b5d
    Glopite: Intrusion? No! No! This is an emergency #line:006237e #line:GlopiteSound

Glopite: Our planet Glopp is being attacked by the Phirakians! #line:0e89aa0#line:GlopiteSound

-> What did you do to provoke the Phirakians? #line:03e64fd 
    Glopite: Provoke? No no! #line:0cabf4d #line:GlopiteSound
    Glopite: The Phirakians attack without reason. They are tyrants #line:03089a1 #line:GlopiteSound
    <<set $typeOfHelp to "earth's support">>
-> That's awful! We must prevent this assault #line:01fcf96 
    Glopite: Many thanks for your concern, captain! #line:0abde92 #line:GlopiteSound
    Glopite: Your help against their forces would be appreciated greatly #line:049ea78 #line:GlopiteSound
    <<set $typeOfHelp to "aid for the rebellion">>
-> What does your planet need? #line:017e61a 
    Glopite: Food and medical care #line:04eb8fa #line:GlopiteSound
    Glopite: Many of us are starving or injured #line:0e1e000 #line:GlopiteSound
    <<set $typeOfHelp to "supplies">>

Glopite: Please say you will help us! #line:053ca7a 

-> We will give you {$typeOfHelp} #line:0c1197b 
    <<set $agreeToHelpGlopites to true>>
    Glopite: You are a blessing to us, human #line:0dbef18 #line:GlopiteSound
    Glopite: You have given us hope! #line:022b245 #line:GlopiteSound
-> We refuse #line:0f3eaa7 
    Glopite: Alas, I'd heard rumours of a Human-Phirakian alliance #line:093ba4f #line:GlopiteSound
    Glopite: But I thought it too tragic to be true #line:00a4f4f #line:GlopiteSound
    Glopite: This is a dark day for the Glopites #line:0a4e668 #line:GlopiteSound

<<leave GroupOfGlopite>>
<<set $scene to 3>>
<<toggle_collider Robot>>
===

title: RobotFinal
tags:
---
Robot: There are many decisions to be made here captain #line:RobotSound2
Robot: The Phiraki will be back soon to discuss terms #line:09ac4ff 
<<if $agreeToHelpGlopites>>
Robot: They will not be happy you are helping Glopites #line:0d07187 
-> That's their problem #line:06d0a65 
    Robot: Phirakian tempers are famously short #line:07b0f04 
    Robot: Be careful #line:0bc098b 
-> I'm sure we can find a solution #line:0574c09 
    Robot: Peace isn't always possible #line:0e1a3a4 
<<else>>
Robot: Turning away the Glopites will give you good favour #line:0e82a9c 
-> The Glopites are a weak species. It is their own fault #line:043a408 
    Robot: A decisive and harsh outlook #line:0143871 
    Robot: Are you sure you are not Phirakian? #line:08274c8 
-> I hope not to lose them as allies #line:0539f9a 
    Robot: You cannot always make everyone happy #line:092b8cd 
<<endif>>

<<if $phirakiansProposeAlliance>>
Robot: The Phiraki proposed an alliance #line:0cfd8dc 
Robot: That would be very beneficial to us #line:0bc17ee 
Robot: Do you intend to accept it? #line:06e2bb9 
<<else>>
Robot: Do you intend to propse an alliance #line:04197bc 
<<endif>>

-> Of course. Our forces will be stronger together #line:0ed5a78 
    Robot: I respect the military mindset #line:019006a 
    Robot: And my data analysis supports this decision #line:067666f 
-> Not with those cruel tyrants #line:0b73265 
    Robot: Your moral and ethical mindset is stronger #line:01eaa59 
    Robot: Admirable, though you shall make many enemies #line:040cb18 

Robot: The Phirakian have returned and will board momentarily #line:06ed30c 
Robot: Good luck Captain #line:0764a87
<<set $scene to 3>>
<<set_object_phirakian_general AlienSpawner>>
<<spawn_object AlienSpawner>>
<<toggle_collider Robot>>
===

title: PhirakianFinal
tags:
---
Hefa: My name is General Hefa and you shall address me as such, human #line:HefaIntro
Hefa: We have many matters that desperately need to be decided #line:PhirakianSound2
<<if $agreeToHelpGlopites == true>>
    <<if $phirakiansInsulted == true>>
        Hefa: Your disrespect is unthinkable
        Hefa: First you treat our representative with no respect
        Hefa: Then agree to give {$typeOfHelp} to the Glopites
        Hefa: A Vile and Primative race
        -> The Glopites are more worthy of respect than bugs like you
            Hefa: Such insolence from such a tiny thing
            Hefa: You should be careful I don't squash you
        -> You overstep your boundary
            Hefa: Your stubborness and arrogance may be tolerated on earth
            Hefa: But on Phira you would be hung
        -> I meant no offense. Only to put earth's best interests first
            Hefa: Selfish in nature. A classic human trait

        Hefa: After much debate, we have only two options for you human
        Hefa: Retreat to earth and recall any aid to Glopp
        Hefa: Refuse and the Phirakian-Human war will continue for another agree
        Hefa: Choose wisely
        -> We'll recall our aid immediately. Glopp is yours
            Hefa: Wise choice. You have postponed our hostilities for now
            <<set $ending to 1>>
            <<set_ending_1 Display>>
        -> Earth will never back down from vicious tyrants
            Hefa: Your ego outgrows you, fool
            Hefa: Your planet will condemn you when war continues
            <<set $ending to 2>>
            <<set_ending_2 Display>>
    <<elseif $phirakiansProposeAlliance == true>>
        Hefa: We have heard of your new found alliance with Glopp
        Hefa: This is concerning for our council
        Hefa: We are sure earth made a mistake and did not mean to insult Phira
        -> No mistake here, General Hefa
            Hefa: Then you may forget our alliance
            Hefa: This decision will have a great future impact
            Hefa: I urge you reconsider
        -> The Glopites are innocent. This invasion is unjust
            Hefa: THe Glopites are mearly slime. This is only pest control
        -> We can be persuaded to back off
            Hefa: You should know there is a hefty reward if you do
            Hefa: This invasion will be most profitable, as will our alliance

        Hefa: So will you recall your forces from Glopp
        Hefa: Our alliance is dependent on this
        -> The alliance is more important. Glopp is yours
            Hefa: A diplomatic and well-thought decision
            Hefa: This is a great day for peace
            <<set $ending to 3>>
            <<set_ending_3 Display>>
        -> We stand with Glopp. Screw your alliance
            Hefa: Then war shall continue to reign between us
            Hefa: I am disappointed. I had such high hopes
            <<set $ending to 2>>
            <<set_ending_2 Display>>
    <<else>>
        Hefa: There is one matter in particular that we find intriguing
        Hefa: An alliance with the Glopites?
        Hefa: A strange development
        -> A necessary one. The Glopites were in need
            Hefa: The Glopites do not need your assistance
            Hefa: They need our rule. Our law. Our more civilised way of living
            Hefa: They will thrive beneath us
        -> We are troubled by your aggression towards them
            Hefa: We only wish to bring order to Glopp
            Hefa: Like we do to all savage planets
        -> Glopite is meerly a pawn
            Hefa: I like your thinking. Very Phirakian
            Hefa: Though it is important you cease your intervention
            Hefa: These affairs are ours alone
        
        Hefa: Cut ties with Glopp, and you shall be awarded kindly
        Hefa: Continue, and we will have no choice but to take care of earth
        -> Very well. We shall stay out of your way
            Hefa: We appreciate your consideration
            Hefa: As for earth, perhaps further peace talks are possible
            <<set $ending to 4>>
            <<set_ending_4 Display>>
        -> We cannot abandon them
            Hefa: And we cannot give up
            Hefa: If earth insists on being a parasite it will be taken care of
            <<set $ending to 5>>
            <<set_ending_5 Display>>
    <<endif>>
<<else>>
    <<if $phirakiansInsulted == true>>
        Hefa: I've been told of your previous insults to my comrade
        Hefa: Though we were pleased to hear you declined the Glopite's plea for help
        Hefa: Such a pathetic excuse for a species
        -> They're not pathetic
            Hefa: Wrong. They are weak. They are meager.
            Hefa: Sometimes I get sick just looking at them
            Hefa: Sorry things
        -> I didn't do it for your joke of an empire
            Hefa: You ought to be more careful with your words
            Hefa: We are still yet to decide earth's fate
        -> I was hoping to make up for my earlier slander
            Hefa: This is the kind of respect we expected all along
            Hefa: Though we cannot forget such rudeness straight away

        Hefa: Phira comes with an offer we believe is irrefusable
        Hefa: Join our assault on Glopp
        Hefa: We shall share the spoils of war
        Hefa: And earth will join Phira in our domination of the galaxy
        Hefa: Turn us down, and earth's fate remains uncertain

        -> We would never be so heartless
            Hefa: Your emotions weaken you
            Hefa: If this is the fate you choose, so be it
            <<set $ending to 1>>
            <<set_ending_1 Display>>
        -> We relish the opportunity. Glopp shall fall
            Hefa: A strong declaration, friend
            Hefa: We welcome earth in their new age as conquerers
            <<set $ending to 6>>
            <<set_ending_6 Display>>
    <<else>>
        Hefa: We applaud your response to the Glopites
        Hefa: They were greedy and selfish to ask you
        Hefa: Our representative speaks highly of you
        Hefa: We would be honoured to have an alliance with earth
        -> What are the terms?
            Hefa: Peace between our worlds. Opportunities for trade routes
            Hefa: Our scientists can work together to form new medicines
            Hefa: Military might can be shared to better protect our worlds
            Hefa: We guarantee earth will become a Utopia
        -> We are hesitant
            Hefa: Please put your apprehensions aside
            Hefa: Phira can offer many benefits to earth
            Hefa: And we hope earth can share some of their own culture in return
        -> You should be ashamed of your assault on Glopp
            Hefa: It is a necessary thing
            Hefa: You may not understand, but Glopites are not as developed as we are
            Hefa: And hence we must show them a better way
        
        Hefa: We are eager to hear your answer
        Hefa: Will you accept our offer of alliance
        Hefa: Or turn down this rare opportunity
        -> We accept your alliance
            Hefa: Excellent! I look forward to working closely together
            <<set $ending to 7>>
            <<set_ending_7 Display>>
        -> We refuse your alliance
            Hefa: Your answer disappoints
            Hefa: We shall continue to attempt relations with your planet
            Hefa: And hope one day you change your mind
            <<set $ending to 4>>
            <<set_ending_4 Display>>
    <<endif>>
<<endif>>

<<leave PhirakianGeneral>>
===