title: Awake
tags:
---
<<declare $scene = 0>>

//ALIEN RACES
// The three main alien races are Phirakians, Glopites and Echolyths
// Phirakians are a hostile military race you are trying to make peace with
// Glopites are a friendly race of slime creatures
// Echolyths are an ancient, peaceful and spiritual people

// As you make peace with the phirakians you are given a choice to "screw over" the glopites
// This may cause the Echolyths to intervene
// by the end the three outcomes are war with Phira, war with Glopites and Echolyths, and neutrality with all three

//Deciders. Each scene decides one of these variables which determine the available endings in the final scene
<<declare $missionExplained = false>>
<<declare $phirakiansProposeAlliance = false>>
<<declare $agreeToHelpGlopites = false>>

<<declare $typeOfHelp = "">>
===

title: Robot
tags:
---
<<if $scene == 0>>
<<jump RobotIntro>>
<<endif>>
===

title: RobotIntro
tags:
---
Robot: Goodmorning Captain! Sleep Well? #line:GoodmorningCaptain 
-> Where am I? #line:04e34c3 
    Robot: Ah, I see the cryo-sleep has affected your memory again #line:Cryosleep 
    Robot: No bother! Long story short, you're aboard a space craft #line:0541f63 
-> Who are you? #line:02718c4 
    Robot: Shoot, your sleeping pod has given you temporary amnesia again. I must get that fixed #line:SleepingPod 
    Robot: I'm Robot, your digital holographic assistant! #line:009527b 
-> What's going on? #line:0787c07 
    Robot: Hmm, seems the sleeping pod hasn't been fixed yet. That'll explain why you can't remember anything #line:SleepingPodNotFixed 
    Robot: You've just awoken from a 2 month cryo sleep #line:03b35ab 
    Robot: We're heading to the planet Phira to make peace talks with the Phirakians #line:078d25b 
    <<set $missionExplained to true>>

Robot: Now that we've caught up... #line:09985ae 
Robot: Welcome to the Scout! The fastest and greatest explorer vessel in the galaxy! #line:0224c5d 
Robot: And you're our captain! #line:004ce1d 

<<if $missionExplained is true>>
-> Vessel? You mean like a space craft? This doesn't look like a space craft though #line:0d49249 
    Robot: That's thanks to the PHOH - Projected Hologram Of Home #line:0e30281 
    Robot: PHOH makes the space deck look a little more familiar to you #line:0946105
-> Will be be on Phira soon? #line:0a2f14a 
    Robot: In a few minutes, affirmative #line:09ea11e
<<else>>
-> Vessel? You mean like a space craft? This doesn't look like a space craft though #line:08bd0a2 
    Robot: That's thanks to the PHOH - Projected Hologram Of Home #line:06134fa 
    Robot: PHOH makes the space deck look a little more familiar to you #line:0534114
-> Okay so, what exactly are we doing? #line:0e29b0e 
    Robot: Currently, we're close to arriving at the planet Phira #line:0c7e7e1 
    Robot: You have been chosen to make peace talks with the Phirakians #line:02ee28b
    <<set $missionExplained to true>>
<<endif>>

<<if $missionExplained == true>>
<<jump PhirakianEncounter>>
<<else>>
<<jump PhirakianSurpriseEncounter>>
<<endif>>

===

title: PhirakianEncounter
tags:
---
Robot: Someone is requesting to board the ship... #line:09830ec
<<set $scene to 1>>
<<set_object_phirakian AlienSpawner>>
<<spawn_object AlienSpawner>>
===

title: PhirakianIntro
tags:
---
Phirakian: Greetings to you, human. My name is Pliink, the Phirakian representative #line:PhirakianGreeting 
<<if $missionExplained == true>>
-> Welcome aboard, Pliink. Make yourself comfortable #line:0467ecf 
    Phirakian: You have my gratitude, Captain. Not all diplomats are as welcoming as you #line:0f85694
    Phirakian: Perhaps Phira would be lucky to have an alliance with your colony after all
    <<set $phirakiansProposeAlliance to true>>
-> Hold on, why can I understand you? #line:0cecdad 
    Phirakian: Simple translation tech developed by Phirakian scientists #line:02e05bf 
    Phirakian: We call it BUTT, Brilliant Universal Translation Technology #line:0801ca1
<<else>>
-> Sorry, who are you? I wasn't expecting any visitors #line:0bb56cb 
    Phirakian: Who am I? Were you not briefed or are you just stupid #line:053b4f3 
    Phirakian: I made the effort to come on behalf of Phira for peace talks... #line:0eb5505 
    Phirakian: And the captain doesn't even know why I'm here! #line:08d5a19 
-> Welcome Pliink! What brings you to our ship? #line:00fee48 
    Phirakian: I was... invited... I'm the diplomat... #line:0e82822 
    Phirakian: Don't tell me you humans are this forgetful #line:07ba4fe 

-> I'm sorry if I've offended you. It's been a long journey. Remind me? #line:096ef08 
    Phirakian: I can forgive your insolance on regard of your efforts at least #line:0ddfaa3 
    Phirakian: Earth and Phira have made the decision to commence peace talks #line:0052ecd 
    Phirakian: After years of devesating war, we have come to offer an alliance #line:01d6bd6 
-> Calm down! You're over reacting! #line:0ca5406 
    Phirakian: How dare you speak such offense, earthling #line:0b02d53 
    Phirakian: I've had enough. I shall retreat to Phira and alert our leaders #line:04a0046 
    Phirakian: I'm sure war is in your planet's future #line:0e21f8e 

<<set $missionExplained to true>>   
<<endif>>

Phirakian: I'm being called by Phira. I must leave for now

<<set $scene to 2>>
<<leave Phirakian>>
<<set_object_glopite AlienSpawner>>
<<spawn_object AlienSpawner>>
===

title: GlopiteIntro
tags:
---
Glopite: Human! Allow me introduce the arrival of the Glopites! #line:0ec92ac 
Glopite: I will pause so you may applaud #line:0ba3620 
-> What brings the Glopite here? #line:087f0fe 
    Glopite: War, human. Terrible war brings us here #line:09cccff 
-> Greetings Glopites? How may we be of service #line:04176c8 
    Glopite: We require your help. We face great danger #line:0dfa95e 
-> We weren't expecting your intrusion #line:0aa9b5d 
    Glopite: Intrusion? No! No! This is an emergency #line:006237e 

Glopite: Our planet Glopp is being attacked by the Phirakians! #line:0e89aa0

-> What did you do to provoke the Phirakians?
    Glopite: Provoke? No no!
    Glopite: The Phirakians attack without reason. They are tyrants
    <<set $typeOfHelp to "earth's support">>
-> That's awful! We must prevent this assault
    Glopite: Many thanks for your concern, captain!
    Glopite: Your help against their forces would be appreciated greatly
    <<set $typeOfHelp to "aid for the rebellion">>
-> What does your planet need?
    Glopite: Food and medical care
    Glopite: Many of us are starving or injured
    <<set $typeOfHelp to "supplies">>

Glopite: Please say you will help us!

-> We will give you {$typeOfHelp}
    <<set $agreeToHelpGlopites to true>>
    Glopite: You are a blessing to us, human
    Glopite: You have given us hope!
-> We refuse
    Glopite: I'd heard rumours of an alliance between you
    Glopite: But I thought it too tragic to be true
    Glopite: This is a dark day for the Glopites

<<leave Glopite>>
===